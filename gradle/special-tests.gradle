apply plugin: 'com.adarshr.test-logger'
def special = [
	'Npm',
	'Black',
	'Clang',
	'Buf'
]

boolean isCiServer = System.getenv().containsKey("CI")
tasks.withType(Test).configureEach {
	// See com.diffplug.spotless.tag package for available JUnit 5 @Tag annotations
	useJUnitPlatform {
		excludeTags special as String[]
	}
	if (isCiServer) {
		retry {
			maxRetries = 2
			maxFailures = 10
		}

		// There are only 2 cores in each GitHub Action Runner, we use all of them here.
		maxParallelForks = Runtime.getRuntime().availableProcessors()
	} else {
		// https://docs.gradle.org/8.4/userguide/performance.html#execute_tests_in_parallel
		maxParallelForks = Runtime.runtime.availableProcessors().intdiv(2) ?: 1
	}
}

special.forEach { tag ->
	tasks.register("test${tag}", Test) {
		useJUnitPlatform { includeTags tag }
	}
}
